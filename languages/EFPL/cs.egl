// Program = functions + main expression
program : { function }* main ;
function : { funsig }? fundef ;
funsig : name '::' texpr ;
fundef : name { name }* '=' expr ;
main : 'main' '=' 'print' '$' expr ;

// Type expressions
[funtype] texpr : typepart { '->' texpr }? ;
[inttype] typepart : 'Int' ;
[booltype] typepart : 'Bool' ;
[typevar] typepart : name ;
[brackettype] typepart : '(' texpr ')' ;

// Expressions
[unary] expr : uop subexpr subexpr ;
[binary] expr : '(' bop ')' subexpr subexpr ;
[subexpr] expr : subexpr ;
[apply] expr : subexpr { subexpr }+ ;
[intconst] subexpr : integer ;
[brackets] subexpr : '(' expr ')' ;
[if] subexpr : 'if' expr 'then' expr 'else' expr ;
[name] subexpr : name ;

// Unary operators
[negate] uop : '-' ;
[not] uop : 'not' ;

// Binary operators
[add] bop : '+' ;
// ...
// BEGIN ...
[sub] bop : '-' ;
[mul] bop : '*' ;
[lt] bop : '<' ;
[le] bop : '<=' ;
[eq] bop : '==' ;
[geq] bop : '>=' ;
[gt] bop : '>' ;
[and] bop : '&&' ;
// END ...
[or] bop : '||' ;

